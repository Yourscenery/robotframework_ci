*** Settings ***
Resource          ../9ShareResource.txt

*** Variables ***
${lightingTableName}    t_lighting    # 数据库中摄像头的表名
&{lighting apikey and dbkey}    projectid=projectid    areaid=areaid    lightingname=lightingname    lightingmodel=lightingmodel    longitude=longitude    latitude=latitude    remark=remark
...               lightingid=lightingid    ##接口请求参数=数据库字段名，用户接口内容与数据库内容断言
${ADD_LIGHTING_SUCCESS}    灯杆新增成功
${UPDATE_LIGHTING_SUCCESS}    灯杆修改成功
${DELETE_LIGHTING_SUCCESS}    灯杆删除成功

*** Keywords ***
lighting_edit_add
    [Arguments]    ${dicParams}    ${message}=${ADD_LIGHTING_SUCCESS}    ${returnKey}=${none}
    [Documentation]    接口没返回灯杆id
    ${url}    set variable    /lighting/lighting/addLighting
    ${dicAssert}    create dictionary    message=${message}
    ${dicResp}    http_post_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    #数据库获取lightingid
    ${tableWhereStatement}    set variable    projectid='${dicParams}[projectid]' and areaid='${dicParams}[areaid]' and lightingname='${dicParams}[lightingname]' and lightingmodel='${dicParams}[lightingmodel]'
    ${sqlLightingId}=    run keyword if    '${message}'=='${ADD_LIGHTING_SUCCESS}'    db_open_query_close    SELECT lightingid FROM ${lightingTableName} where ${tableWhereStatement}
    ${lenSqlLightingId}=    run keyword if    '${message}'=='${ADD_LIGHTING_SUCCESS}'    get length    ${sqlLightingId}
    ${lightingId}=    run keyword if    '${message}'=='${ADD_LIGHTING_SUCCESS}' and '${lenSqlLightingId}'=='1'    set variable    ${sqlLightingId[0][0]}
    ...    ELSE IF    '${message}'=='${ADD_LIGHTING_SUCCESS}' and '${lenSqlLightingId}'!='1'    fail    【数据库获取新增门禁id数量不等于1---${sqlLightingId}】
    #数据库断言；没返回门禁id
    #获取每列的值拼接作为where语句
    ${tableFromWhere}=    run keyword if    '${lightingId}'!='${none}' and '${lightingId}'!='${empty}'    set variable    ${lightingTableName} WHERE lightingid='${lightingId}' and areaid='${dicParams}[areaid]' and lightingname='${dicParams}[lightingname]' and lightingmodel='${dicParams}[lightingmodel]'
    log    ${tableFromWhere}
    run keyword if    '${lightingId}'!='${empty}' and '${lightingId}'!='${none}' and '${message}'=='${ADD_LIGHTING_SUCCESS}'    assert_response_database_add_or_update    ${lighting apikey and dbkey}    ${dicParams}    ${tableFromWhere}
    ...    ELSE    log    【没新增成功不走数据库断言】
    [Return]    ${lightingId}

lighting_edit_add_oneData
    [Arguments]    ${mark}=a
    [Documentation]    固定数据新增一条灯杆数据，用于更新、删除接口使用
    log    ---新增一条灯杆数据并获取其id---
    ${time}=    get_time_no_year
    ${dicParams}    create dictionary    projectid=${G_PROJECT_ID}    areaid=${G_AREA_ID}    lightingname=o${mark}杆${time}    lightingmodel=${time}    longitude=120.19203
    ...    latitude=30.192175    remark=${mark}${time}    address=浙江省杭州市滨江区长河街道滨兴路1734号
    ${sqlLightingId}=    lighting_edit_add    ${dicParams}
    [Return]    ${sqlLightingId}

lighting_edit_query
    [Arguments]    ${dicParams}    ${total}=0    ${returnKey}=records
    ${url}    set variable    /lighting/lighting/getLightingList
    ${dicAssert}    create dictionary    total=${total}
    ${respKeyVal}    http_get_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    ${lenRespKeyVal}    get length    ${respKeyVal}
    ${queryLightingId}=    run keyword if    ${lenRespKeyVal}>0    set variable    ${respKeyVal}[0][lightingid]
    ...    ELSE    set variable    ${None}
    #数据库断言------获取每列的值拼接作为where语句
    comment    run keyword if    ${lenRespKeyVal}>0    assert_response_database_query    ${lighting apikey and dbkey}    ${respKeyVal}    ${lightingTableName}
    ...    lightingid
    ...    ELSE    log    【没查询数据不走数据库断言】
    [Return]    ${queryLightingId}

lighting_edit_update
    [Arguments]    ${dicParams}    ${message}=${UPDATE_LIGHTING_SUCCESS}    ${returnKey}=${none}
    [Documentation]    接口没返回灯杆id
    ${url}    set variable    /lighting/lighting/updateLighting
    ${dicAssert}    create dictionary    message=${message}
    ${updateLightingID}    http_post_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    #数据库断言
    #获取每列的值拼接作为where语句
    ${tableFromWhere}    set variable    ${lightingTableName} WHERE lightingid='${dicParams}[lightingid]' and areaid='${dicParams}[areaid]' and lightingname='${dicParams}[lightingname]' and lightingmodel='${dicParams}[lightingmodel]'
    run keyword if    '${message}'=='${UPDATE_LIGHTING_SUCCESS}'    assert_response_database_add_or_update    ${lighting apikey and dbkey}    ${dicParams}    ${tableFromWhere}
    ...    ELSE    log    【没修改成功不走数据库断言】
    [Return]    ${dicParams}[lightingid]

lighting_edit_delete
    [Arguments]    ${dicParams}    ${message}=${DELETE_LIGHTING_SUCCESS}
    ${url}    set variable    /lighting/lighting/delLighting
    ${dicAssert}    create dictionary    message=${message}
    http_post    ${url}    ${dicParams}    ${dicAssert}
    #数据库断言
    ${lightingid}    get from dictionary    ${dicParams}    lightingIds[]
    ${tableSelectFrom}    set variable    select lightingid,1 from ${lightingTableName}
    run keyword if    '${lightingid}'!='${none}' and '${message}'=='${DELETE_LIGHTING_SUCCESS}'    assert_response_database_delete    ${tableSelectFrom}    lightingid    ${lightingid}
    ...    ELSE    log    【没删除成功不走数据库断言】

lighting_device_notbinding
    [Arguments]    ${deviceName}    ${dicParams}    ${total}=0    ${returnKey}=records
    [Documentation]    未绑定设备接口，根据${device}值判断走哪个接口；工控机：envir；灯具：bright；广告屏：adscreen；摄像头：camera；一键呼叫：alarm；音柱：spon；
    ${url}=    run keyword if    '${deviceName}'=='envir'    set variable    /lighting/lighting/sensorNotBind
    ...    ELSE IF    '${deviceName}'=='bright'    set variable    /lighting/lighting/brightNotBind
    ...    ELSE IF    '${deviceName}'=='adscreen'    set variable    /lighting/lighting/adscreenNotBind
    ...    ELSE IF    '${deviceName}'=='camera'    set variable    /lighting/lighting/cameraNotBind
    ...    ELSE IF    '${deviceName}'=='alarm'    set variable    /lighting/lighting/alarmNotBind
    ...    ELSE IF    '${deviceName}'=='spon'    set variable    /lighting/lighting/sponNotBind
    ...    ELSE    fail    【${deviceName}】不在【工控机envir；灯具bright；广告屏adscreen；摄像头camera；一键呼叫alarm；音柱spon；】里
    ${dicAssert}    create dictionary    total=${total}
    ${respKeyVal}    http_get_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    ${lenRespKeyVal}    get length    ${respKeyVal}
    ${queryEnvirId}=    run keyword if    ${lenRespKeyVal}>0    set variable    ${respKeyVal}[0][lightingid]
    ...    ELSE    set variable    ${None}
    [Return]    ${queryEnvirId}

lighting_device_bind_lose
    [Arguments]    ${dicParams}    ${type}=参考：lose/bind    ${message}=参考：灯杆绑定成功/灯杆解绑成功
    [Documentation]    绑定+解绑设备接口
    ${url}    set variable if    '${type}'=='bind'    /lighting/lighting/deviceBinding    /lighting/lighting/deviceLoseBinding
    ${dicAssert}    create dictionary    message=${message}
    http_post    ${url}    ${dicParams}    ${dicAssert}
    ###数据库断言：在灯杆表中，每个灯杆数据中有绑定设备字段，字段空：未绑定，非空：绑定；
    ###判断传参值与数据库字段值是否相等
