*** Settings ***
Resource          ../9ShareResource.txt

*** Variables ***
${envirTableName}    t_envir_devices    # 数据库中工控机的表名
##envir apikey and dbkey接口请求参数=数据库字段名，用户接口内容与数据库内容断言
&{envir apikey and dbkey}    projectId=project_id    areaId=area_id    title=device_name    auth_info=device_id    type=device_type    x=x    y=y
...               remarks=remarks
${ADD_ENVIR_SUCCESS}    ${True}
${PAGE_ENVIR_SUCCESS}    成功
${UPDATE_ENVIR_SUCCESS}    编辑成功
${DELETE_ENVIR_SUCCESS}    删除成功

*** Keywords ***
envir_edit_add
    [Arguments]    ${dicParams}    ${success}=${ADD_ENVIR_SUCCESS}    ${returnKey}=object
    ${url}    set variable    /envir/device/add
    ${dicAssert}    create dictionary    success=${success}
    ${returnValue}    http_post_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    ${bIn}=    evaluate    '.' in '${returnKey}'
    ${envirId}    set variable if    ${bIn}==${false}    ${returnValue}[deviceId]    ${returnValue}
    run keyword if    '${envirId}'!='${none}' and '${success}'=='${ADD_ENVIR_SUCCESS}'    set suite variable    ${S_ADD_ENVIR_ID}    ${envirId}
    #数据库断言--获取每列的值拼接作为where语句
    ${tableFromWhere}    set variable    ${envirTableName} WHERE area_id='${dicParams}[areaId]' and device_name='${dicParams}[title]' and device_id='${dicParams}[auth_info]'
    run keyword if    '${envirId}'!='${none}' and '${success}'=='${ADD_ENVIR_SUCCESS}'    assert_response_database_add_or_update    ${envir apikey and dbkey}    ${dicParams}    ${tableFromWhere}
    ...    ELSE    log    ***pass【没新增成功不走数据库断言】***
    [Return]    ${envirId}

envir_edit_add_oneData
    [Arguments]    ${mark}=a
    [Documentation]    固定数据新增一条工控机数据，用于更新、删除接口使用
    log    ---新增一条工控机数据并获取其id---
    ${time}=    get_time_no_year
    ${dicParams}    create dictionary    projectId=${G_PROJECT_ID}    areaId=${G_AREA_ID}    title=o${mark}工${time}    auth_info=o${mark}${time}    type=2
    ...    x=1.0    y=1.0    remarks=${mark}${time}
    ${envirId}=    envir_edit_add    ${dicParams}
    [Return]    ${envirId}

envir_edit_query
    [Arguments]    ${dicParams}    ${total}=0    ${returnKey}=records
    ${url}    set variable    /envir/envir/query
    ${dicAssert}    create dictionary    total=${total}
    ${respKeyVal}    http_post_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    ${lenRespKeyVal}    get length    ${respKeyVal}
    #获取第一行数据id
    ${envirId}=    run keyword if    ${lenRespKeyVal}>0    set variable    ${respKeyVal}[0][id]
    ...    ELSE    set variable    ${None}
    #数据库断言------获取每列的值拼接作为where语句
    #${tableWhereStatement}    set variable    id in (${envirId})
    run keyword if    ${lenRespKeyVal}>0    assert_response_database_query    ${envir apikey and dbkey}    ${respKeyVal}    ${envirTableName}    id
    ...    ELSE    log    ***pass【没查询数据不走数据库断言】***
    [Return]    ${envirId}

envir_edit_pageCondition
    [Arguments]    ${dicParams}    ${message}=${PAGE_ENVIR_SUCCESS}    ${returnKey}=object.records
    ${url}    set variable    /envir/envir/page-condition
    ${dicAssert}    create dictionary    message=${message}
    ${respKeyVal}    http_get_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    ${lenRespKeyVal}    get length    ${respKeyVal}
    #获取第一行数据id
    ${envirId}=    run keyword if    ${lenRespKeyVal}>0    set variable    ${respKeyVal}[0][id]
    ...    ELSE    set variable    ${None}    #数据库断言------获取每列的值拼接作为where语句    #${tableWhereStatement}    set variable
    ...    # id in (${envirId})    #run keyword if    ${lenRespKeyVal}>0    assert_response_database_query    ${envir apikey and dbkey}    ${respKeyVal}
    ...    # ${envirTableName}    id    # ELSE    log    ***pass【没查询数据不走数据库断言】***
    [Return]    ${envirId}

envir_edit_update
    [Arguments]    ${dicParams}    ${message}=${UPDATE_ENVIR_SUCCESS}    ${returnKey}=object.id
    ${url}    set variable    /envir/envir/update
    ${dicAssert}    create dictionary    message=${message}
    ${envirId}    http_post_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    #数据库断言--获取每列的值拼接作为where语句
    ${tableFromWhere}    Set Variable If    '${envirId}'!='${none}' and '${message}'=='${UPDATE_ENVIR_SUCCESS}'    ${envirTableName} WHERE id='${envirId}' and area_id='${dicParams}[areaId]' and device_name='${dicParams}[deviceName]' and device_number='${dicParams}[deviceNumber]'
    run keyword if    '${envirId}'!='${none}' and '${message}'=='${UPDATE_ENVIR_SUCCESS}'    assert_response_database_add_or_update    ${envir apikey and dbkey}    ${dicParams}    ${tableFromWhere}
    ...    ELSE    log    ***pass【没更新成功不走数据库断言】***
    [Return]    ${envirId}

envir_edit_delete
    [Arguments]    ${dicParams}    ${message}=${DELETE_ENVIR_SUCCESS}
    ${url}    set variable    /envir/envir/delete
    ${dicAssert}    create dictionary    message=${message}
    http_post    ${url}    ${dicParams}    ${dicAssert}
    #数据库断言
    ${ids}    get from dictionary    ${dicParams}    ids[]
    #tableSelectFrom，作为select 的查询字段；
    ${tableSelectFrom}    set variable    select id,deleted from ${envirTableName}
    run keyword if    '${ids}'!='${none}' and '${message}'=='${DELETE_ENVIR_SUCCESS}'    assert_response_database_delete    ${tableSelectFrom}    id    ${ids}
    ...    ELSE    log    ***pass【没删除成功不走数据库断言】***

envir_overview_status_num
    [Arguments]    ${success}=True    ${returnKey}=object
    [Documentation]    设备总览-工控机设备在离线统计
    ${url}    set variable    /envir/envir/statusNum
    ${dicAssert}    create dictionary    success=${success}
    ${respKeyVal}    http_post_return    ${url}    ${none}    ${dicAssert}    ${returnKey}
    log    ***${respKeyVal}[onlineNum]在线；${respKeyVal}[offlineNum]离线；
    [Return]    ${respKeyVal}

envir_overview_getEnvirListByLighting
    [Arguments]    ${dicParams}    ${total}=0    ${returnKey}=records
    [Documentation]    设备总览-工控机列表 deviceAlias=3&size=10current=2
    ${url}    set variable    /lighting/lighting/getEnvirListByLighting
    ${dicAssert}    create dictionary    total=${total}
    ${respKeyVal}    http_post_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    ${lenRespKeyVal}    get length    ${respKeyVal}
    #获取第一行数据id
    ${envirId}=    run keyword if    ${lenRespKeyVal}>0    set variable    ${respKeyVal}[0][id]
    ...    ELSE    set variable    ${None}
    [Return]    ${envirId}

envir_overview_status
    [Arguments]    ${dicParams}    ${success}=True    ${returnKey}=object
    [Documentation]    设备总览-工控机设备-查询ids[]: 0884a4366bd799a33671611c015e3bd0,
    ${url}    set variable    /envir/envir/status
    ${dicAssert}    create dictionary    success=${success}
    ${respKeyVal}    http_post_return    ${url}    ${dicParams}    ${dicAssert}    ${returnKey}
    [Return]    ${respKeyVal}
